<emu-clause id=sec-syntax>
  <h1>Syntax</h1>
  <emu-grammar>

    PropertyDefinition[Yield, Await] :
      IdentifierReference[?Yield, ?Await]
      CoverInitializedName[?Yield, ?Await]
      PropertyName[?Yield, ?Await] `:` AssignmentExpression[+In, ?Yield, ?Await]
      MethodDefinition[?Yield, ?Await]
      <ins>`hidden` MethodDefinition[?Yield, ?Await]</ins>

    ClassElement[Yield, Await] :
      <ins>InstanceVariableDefinition</ins>
      MethodDefinition[?Yield, ?Await]
      <ins>`hidden` MethodDefinition[?Yield, ?Await]</ins>
      `static` MethodDefinition[?Yield, ?Await]
      <ins>`static` `hidden` MethodDefinition[?Yield, ?Await]</ins>
      <ins>`static` `{` StatementList[~Yield, ~Await] `}`</ins>
      `;`

    InstanceVariableDefinition :
      `var` InstanceVariableList `;`

    InstanceVariableList :
      BindingIdentifier
      InstanceVariableList `,` BindingIdentifier

    MemberExpression[Yield, Await] :
      PrimaryExpression[?Yield, ?Await]
      MemberExpression[?Yield, ?Await] `[` Expression[+In, ?Yield, ?Await] `]`
      MemberExpression[?Yield, ?Await] `.` IdentifierName
      <ins>MemberExpression[?Yield, ?Await] `->` IdentifierName</ins>
      MemberExpression[?Yield, ?Await] TemplateLiteral[?Yield, ?Await]
      SuperProperty[?Yield, ?Await]
      MetaProperty
      `new` MemberExpression[?Yield, ?Await] Arguments[?Yield, ?Await]

    CallExpression[Yield, Await] :
      CoverCallExpressionAndAsyncArrowHead[?Yield, ?Await] #callcover
      SuperCall[?Yield, ?Await]
      CallExpression[?Yield, ?Await] Arguments[?Yield, ?Await]
      CallExpression[?Yield, ?Await] `[` Expression[+In, ?Yield, ?Await] `]`
      CallExpression[?Yield, ?Await] `.` IdentifierName
      <ins>CallExpression[?Yield, ?Await] `->` IdentifierName</ins>
      CallExpression[?Yield, ?Await] TemplateLiteral[?Yield, ?Await]

  </emu-grammar>
</emu-clause>
